buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

def buildNumber = System.env.CI_JOB_ID ?: "0"
def fabricApiKey = System.env.FABRIC_API_KEY ?: ""

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

if (!fabricApiKey.isEmpty()) {
    apply plugin: 'io.fabric'
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    jcenter()
    google()
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.1'

    defaultConfig {
        applicationId "app.devlife.connect2sql"
        versionCode 40_00_10
        versionName "4.0.1"

        minSdkVersion 23
        targetSdkVersion 27

        manifestPlaceholders = [fabricApiKey: fabricApiKey]

        project.version = versionName
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/services/java.sql.Driver'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        release {
            keyAlias "connect2sql"
            keyPassword System.env.SIGNING_KEY_PASSWORD
            storeFile file("${project.projectDir}/../devtools/signing/release.keystore")
            storePassword System.env.SIGNING_STORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "+d${buildNumber}"
            minifyEnabled false
        }

        release {
            versionNameSuffix "+r${buildNumber}"
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "connect2sql-${variant.buildType.name}-${variant.versionName}.apk"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {

    }

    lintOptions {
        abortOnError false
    }
}

ext {
    version_answers = '1.4.2'
    version_crashlytcs = '2.9.4'
    version_dagger = '2.16'
    version_jsr250 = '1.0'
    version_logback_android = '1.1.1-12'
    version_mariadb = '1.7.4'
    version_dexmaker_mockito = '2.19.0'
    version_patternlock = '2.0.1'
    version_rx = '1.1.0'
    version_saripaar = '1.0.3'
    version_sqlcipher = '3.5.9'
    version_slf4j = '1.7.25'
    version_support = '27.1.1'
}

dependencies {
    implementation "com.mobsandgeeks:android-saripaar:${version_saripaar}"
    implementation "com.android.support:support-v4:${version_support}"
    implementation "com.android.support:support-annotations:${version_support}"
    implementation "com.android.support:appcompat-v7:${version_support}"
    implementation "com.android.support:cardview-v7:${version_support}"
    implementation "com.android.support:design:${version_support}"
    implementation "com.android.support:recyclerview-v7:${version_support}"
    implementation("com.crashlytics.sdk.android:answers:${version_answers}@aar") {
        transitive = true;
    }
    implementation("com.crashlytics.sdk.android:crashlytics:${version_crashlytcs}@aar") {
        transitive = true
    }
    implementation "com.github.tony19:logback-android:${version_logback_android}"
    kapt "com.google.dagger:dagger-android-processor:${version_dagger}"
    kapt "com.google.dagger:dagger-compiler:${version_dagger}"
    implementation "com.google.dagger:dagger:${version_dagger}"
    implementation "com.google.dagger:dagger-android:${version_dagger}"
    implementation "com.google.dagger:dagger-android-support:${version_dagger}"
    implementation "com.linkedin.dexmaker:dexmaker-mockito:${version_dexmaker_mockito}"
    implementation "javax.annotation:jsr250-api:${version_jsr250}"
    implementation "io.reactivex:rxandroid:${version_rx}"
    implementation "io.reactivex:rxjava:${version_rx}"
    implementation "me.zhanghai.android.patternlock:library:${version_patternlock}"
    implementation "net.zetetic:android-database-sqlcipher:${version_sqlcipher}"
    implementation "org.mariadb.jdbc:mariadb-java-client:${version_mariadb}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "org.slf4j:slf4j-api:${version_slf4j}"
}
